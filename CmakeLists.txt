cmake_minimum_required(VERSION 3.31)
project(MySDLGame)

set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set_property(GLOBAL PROPERTY USE_FOLDERS ON)

add_subdirectory(src/Modules/Math)
add_subdirectory(src/Modules/Core)
add_subdirectory(src/Modules/Events)
add_subdirectory(src/Modules/SDL)

file(GLOB_RECURSE SOURCES 
    "src/*.cpp" 
    "src/*.h"
    "src/*.cxx"
)

list(FILTER SOURCES EXCLUDE REGEX "src/Modules/.*")
list(APPEND SOURCES ${CORESDL_MODULE_SOURCES})

add_executable(MyGame ${SOURCES})

source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}/src" PREFIX "Source Files" FILES ${SOURCES})

source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}/src/Modules/SDL/src" PREFIX "Source Files/CoreSDL" FILES ${CORESDL_MODULE_SOURCES})

target_include_directories(MyGame PRIVATE 
    ${CMAKE_CURRENT_SOURCE_DIR}/src
    ${CORESDL_MODULE_INCLUDE_DIRS}
)

target_precompile_headers(MyGame PRIVATE src/pch.h)

target_link_libraries(MyGame PRIVATE 
    CoreModule
    MathModule
    EventsModule
    ${CORESDL_MODULE_LIBRARIES}
)

# Копирование SDL3 DLL рядом с исполняемым файлом (для Windows, если используется SDL3-shared)
if(WIN32)
    if(TARGET ${CORESDL_SDL3_TARGET})
        # Проверяем, является ли таргет shared библиотекой
        get_target_property(SDL3_TYPE ${CORESDL_SDL3_TARGET} TYPE)
        if(SDL3_TYPE STREQUAL "SHARED_LIBRARY")
            add_custom_command(TARGET MyGame POST_BUILD
                COMMAND ${CMAKE_COMMAND} -E copy_if_different
                $<TARGET_FILE:${CORESDL_SDL3_TARGET}>
                $<TARGET_FILE_DIR:MyGame>
            )
        endif()
    endif()
endif()

if(MSVC)
    set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT MyGame)
endif()